#!perl

# NO_PERINCI_CMDLINE_SCRIPT
# FRAGMENT id=shcompgen-hint command=_cpanm-script

# DATE
# VERSION

use strict;
use warnings;

use App::lcpan;
use Perinci::CmdLine::Util::Config;

my $res = Perinci::CmdLine::Util::Config::read_config(
    program_name => "lcpan",
);
die "Can't read lcpan config files: $res->[0] - $res->[1]\n"
    unless $res->[0] == 200;
my $config = $res->[2];

my %args;
$res = Perinci::CmdLine::Util::Config::get_args_from_config(
    config => $config,
    args   => \%args,
    subcommand_name => 'update-index',
    meta   => $App::lcpan::SPEC{update},
);
die "Can't set lcpan function argument from config: $res->[0] - $res->[1]\n"
    unless $res->[0] == 200;
App::lcpan::_set_args_default(\%args);
my $dbh = App::lcpan::_connect_db('ro', $args{cpan}, $args{index_name}, $args{use_bootstrap});

my %modnames; # key = scriptname
{
    for (@ARGV) {
        next unless /\A[A-Za-z0-9_][A-Za-z0-9_-]*\z/;
        $modnames{$_} = undef;
    }
    my $scripts_s = join(",",map {"'$_'"} sort keys %modnames);
    last unless length $scripts_s;
    my $sth = $dbh->prepare("
SELECT
  script.name script,
  (SELECT name FROM module WHERE file_id=file.id LIMIT 1) module
FROM script
LEFT JOIN file   ON script.file_id=file.id
WHERE script.name IN ($scripts_s)
");
    $sth->execute;
    while (my @row = $sth->fetchrow_array) {
        $modnames{$row[0]} = $row[1];
    }
}

{
    local $ENV{PERL5OPT} = $ENV{LCPANM_PERL5OPT}
        if defined $ENV{LCPANM_PERL5OPT};

    my @cmd = ("cpanm");
    push @cmd, "--mirror", $args{cpan}, "--mirror-only" if $args{cpan}; # should be defined
    push @cmd, map { exists $modnames{$_} ? $modnames{$_} : $_ } @ARGV;

    print "lcpanm: exec: ".join(" ", @cmd), "\n" if $ENV{DEBUG};
    exec @cmd;
}

# ABSTRACT: cpanm wrapper (set mirror to local CPAN by default, convert script names to module names)
# PODNAME:

=head1 SYNOPSIS

Use like you would use L<cpanm-script>:

 % lcpanm-script -n somescript ...


=head1 DESCRIPTION

C<lcpanm-script> is just like L<cpanm-script>, but also adds C<--mirror $cpan>
and C<--mirror-only>, where I<$cpan> is path to the local CPAN mirror read from
F<lcpan.conf>.


=head1 ENVIRONMENT

=head2 LCPANM_PERL5OPT

If set, will set C<PERL5OPT> to this value when executing L<cpanm>.


=head1 SEE ALSO

L<cpanm-script>, L<cpanm>

L<App::lcpan> and L<lcpan>
